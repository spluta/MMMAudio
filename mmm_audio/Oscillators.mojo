from std.math import sin, floor
from mmm_audio import *

struct Phasor[num_chans: Int = 1](Movable, Copyable):
    """Phasor Oscillator.

    An oscillator that generates a ramp waveform from 0.0 to 1.0. The phasor is the root of all oscillators in MMMAudio.
    
    The Phasor can act as a simple phasor with the .next() function. 
    
    However, it can also be an impulse with next_impulse() and a boolean impulse with next_bool().

    Parameters:
        num_chans: Number of channels.
    """
    var phase: MFloat[Self.num_chans]
    var freq_mul: Float64
    var rising_bool_detector: RisingBoolDetector[Self.num_chans]
    var rising_bool_detector_impulse: RisingBoolDetector[Self.num_chans]
    var world: World  # Pointer to the MMMWorld instance

    def __init__(out self, world: World):
        """Initialize the phasor oscillator.

        Args:
            world: Pointer to the MMMWorld instance.
        """
        self.world = world
        self.phase = MFloat[Self.num_chans](0.0)
        self.freq_mul = 1.0 / self.world[].sample_rate
        self.rising_bool_detector = RisingBoolDetector[Self.num_chans]()
        self.rising_bool_detector_impulse = RisingBoolDetector[Self.num_chans]()

    @doc_hidden
    @always_inline
    def _increment_phase(mut self: Phasor, freq: MFloat[self.num_chans]):
        self.phase += (freq * self.freq_mul)
        self.phase = self.phase - floor(self.phase)
        
    @doc_hidden
    @always_inline
    def _increment_phase_impulse(mut self, freq: MFloat[self.num_chans], phase_offset: MFloat[self.num_chans] = 0.0) -> MBool[Self.num_chans]:
        """Increments the phase and returns a boolean SIMD indicating when the phase wraps around from 1.0 to 0.0, which is when an impulse would occur. This only works with possive frequencies and phase offsets between 0.0 and 1.0."""

        self.phase += (freq * self.freq_mul)
        fl = floor(self.phase)
        rbd = self.rising_bool_detector_impulse.next(abs(self.phase+clip(phase_offset, 0.0, 1.0)).gt(1.0))
        self.phase = self.phase - fl 
        return rbd

    @always_inline
    def next(mut self: Phasor, freq: MFloat[self.num_chans] = 100.0, phase_offset: MFloat[self.num_chans] = 0.0, trig: MBool[self.num_chans] = MBool[self.num_chans](fill=True)) -> MFloat[self.num_chans]:
        """Creates the next sample of the phasor output based on the inputs.

        Args:
          freq: Frequency of the phasor in Hz.
          phase_offset: Offsets the phase of the oscillator (0 to 1).
          trig: Trigger signal to reset the phase when switching from False to True.

        Returns:
            The next sample of the phasor output.
        """

        self._increment_phase(freq)

        
        var resets = self.rising_bool_detector.next(trig)
        self.phase = resets.select(0.0, self.phase)

        return (self.phase + phase_offset) % 1.0
            
    @always_inline
    def next_bool(mut self, freq: MFloat[self.num_chans] = 100.0, phase_offset: MFloat[self.num_chans] = 0.0, trig: MBool[self.num_chans] = MBool[self.num_chans](fill=True)) -> MBool[self.num_chans]:
        """Increments the phasor and returns a boolean impulse when the phase wraps around from 1.0 to 0.0. This only works with possive frequencies and phase offsets between 0.0 and 1.0.

        Args:
          freq: Frequency of the phasor in Hz (default is 100.0).
          phase_offset: Offsets the phase of the oscillator (default is 0.0).
          trig: Trigger signal to reset the phase when switching from False to True (default is all True, which resets the phasor on the first sample).

        Returns:
            A boolean SIMD indicating True when the impulse occurs.
        """

        tick = self._increment_phase_impulse(freq, phase_offset)
        rbd = self.rising_bool_detector.next(trig)
        self.phase = rbd.select(0.0, self.phase)
        
        return (tick | rbd)

    @always_inline
    def next_impulse(mut self, freq: MFloat[self.num_chans] = 100.0, phase_offset: MFloat[self.num_chans] = 0.0, trig: MBool[self.num_chans] = MBool[self.num_chans](fill=   True)) -> MFloat[self.num_chans]:
        """Generates an impulse waveform where the output is 1.0 for one sample when the phase wraps around from 1.0 to 0.0, and 0.0 otherwise. This only works with possive frequencies and phase offsets between 0.0 and 1.0.

        Args:
          freq: Frequency of the phasor in Hz (default is 100.0).
          phase_offset: Offsets the phase of the oscillator (default is 0.0).
          trig: Trigger signal to reset the phase when switching from False to True (default is all True, which resets the phasor on the first sample).

        Returns:
            The next impulse sample as a Float64. 1.0 when the impulse occurs, 0.0 otherwise.
        """
        tick = self._increment_phase_impulse(freq, phase_offset)
        rbd = self.rising_bool_detector.next(trig)
        self.phase = rbd.select(0.0, self.phase)
        
        return (tick | rbd).cast[DType.float64]()


struct Impulse[num_chans: Int = 1](Movable, Copyable):
    """Impulse Oscillator.

    An oscillator that outputs a 1.0 or True for one sample when the phase wraps around from 1.0 to 0.0.
    
    Impulse is essentially a wrapper around the Phasor oscillator that provides impulse-specific methods.

    Parameters:
        num_chans: Number of channels (default is 1).
    """
    var phasor: Phasor[Self.num_chans]

    def __init__(out self, world: World):
        """Initialize the impulse oscillator.

        Args:
            world: Pointer to the MMMWorld instance.
        """
        self.phasor = Phasor[self.num_chans](world)

    @always_inline
    def next_bool(mut self, freq: MFloat[self.num_chans] = 100.0, phase_offset: MFloat[self.num_chans] = 0.0, trig: MBool[self.num_chans] = MBool[self.num_chans](fill=True)) -> MBool[self.num_chans]:
         """Increments the phasor and returns a boolean impulse when the phase wraps around from 1.0 to 0.0.

        Args:
          freq: Frequency of the phasor in Hz (default is 100.0).
          phase_offset: Offsets the phase of the oscillator (default is 0.0).
          trig: Trigger signal to reset the phase when switching from False to True (default is all True, which resets the phasor on the first sample).

        Returns:
            A boolean SIMD indicating True when the impulse occurs.
        """
        return self.phasor.next_bool(freq, phase_offset, trig) 

    @always_inline
    def next(mut self, freq: MFloat[self.num_chans] = 100.0, phase_offset: MFloat[self.num_chans] = 0.0, trig: MBool[self.num_chans] = MBool[self.num_chans](fill= True)) -> MFloat[self.num_chans]:
        """Generates an impulse waveform where the output is 1.0 for one sample when the phase wraps around from 1.0 to 0.0, and 0.0 otherwise.

        Args:
          freq: Frequency of the phasor in Hz (default is 100.0).
          phase_offset: Offsets the phase of the oscillator (default is 0.0).
          trig: Trigger signal to reset the phase when switching from False to True (default is all True, which resets the phasor on the first sample).

        Returns:
            The next impulse sample as a Float64. 1.0 when the impulse occurs, 0.0 otherwise.
        """
        return self.phasor.next_impulse(freq, phase_offset, trig) 


struct Osc[num_chans: Int = 1, interp: Interp = Interp.linear, ov_samp: TimesOversampling = TimesOversampling.none](Movable, Copyable):
    """Wavetable Oscillator Core.

    A wavetable oscillator capable of all standard waveforms and also able to load custom wavetables. Capable of linear, cubic, quadratic, lagrange, or sinc interpolation. Also capable of using an internal [Downsampler](Downsampler.md).
    
    - Pure tones can be generated without oversampling or sinc interpolation.
    - When doing extreme modulation, best practice is to use sinc interpolation and an TimesOversampling.x2.
    - Try all the combinations of interpolation and oversampling to find the best tradeoff between quality and CPU usage for your application.

    Parameters:
        num_chans: Number of channels (default is 1).
        interp: Interpolation method. See [Interp](MMMWorld.md/#struct-interp) struct for options (default is Interp.linear).
        ov_samp: A [TimesOversampling](MMMWorld.md#struct-timesoversampling) struct to indicate times oversampling.
    """

    var phasor: Phasor[Self.num_chans]
    var world: World 
    var freq_upsampler: Optional[Upsampler[Self.num_chans, Self.ov_samp]]
    var phase_upsampler: Optional[Upsampler[Self.num_chans, Self.ov_samp]]
    var downsampler: Optional[Downsampler[Self.num_chans, Self.ov_samp]]
    var last_phase: MFloat[Self.num_chans]

    def __init__(out self, world: World):
        """Initialize the oscillator.

        Args:
            world: Pointer to the MMMWorld instance.
        """
        self.world = world
        if Self.ov_samp == TimesOversampling.none:
            self.phasor = Phasor[self.num_chans](world)
            self.freq_upsampler = None
            self.phase_upsampler = None
            self.downsampler = None
        else:
            oversampled_world = world[].create_subworld(Self.ov_samp)
            self.phasor = Phasor[self.num_chans](oversampled_world)
            self.freq_upsampler = Upsampler[self.num_chans, Self.ov_samp](world)
            self.phase_upsampler = Upsampler[self.num_chans, Self.ov_samp](world)
            self.downsampler = Downsampler[self.num_chans, Self.ov_samp](world)

        self.last_phase = MFloat[self.num_chans](0.0)

    @always_inline
    def next[osc_type: OscType = OscType.sine](
            mut self: Osc, 
            freq: MFloat[self.num_chans] = MFloat[self.num_chans](100.0), 
            phase_offset: MFloat[self.num_chans] = MFloat[self.num_chans](0.0), 
            trig: Bool = False
        ) -> MFloat[self.num_chans]:
        """
        Generate the next oscillator sample on a single waveform type. All inputs are SIMD types except trig, which is a scalar. This means that an oscillator can have num_chans different instances, each with its own frequency, phase offset, and waveform type, but they will all share the same trigger signal.

        Parameters:
            osc_type: Type of waveform. See the OscType struct for options (default is OscType.sine).

        Args:
            freq: Frequency of the oscillator in Hz.
            phase_offset: Offsets the phase of the oscillator (0 to 1).
            trig: Trigger signal to reset the phase when switching from False to True (default is 0.0).

        Returns:
            The next sample of the oscillator output.
        """
        
        var trig_mask = MBool[self.num_chans](fill=trig)
            
        out = MFloat[self.num_chans](0.0)

        comptime if Self.ov_samp == TimesOversampling.none:
            phase = self.phasor.next(freq, phase_offset, trig_mask)
            ref temp = self.world[].osc_buffers()
            comptime for chan in range(self.num_chans):
                out[chan] = temp.at_phase[osc_type, self.interp](self.world, phase[chan], self.last_phase[chan])
            self.last_phase = phase
            return out
        else:
            comptime for i in range(Self.ov_samp.times):
                freq2 = self.freq_upsampler.value().next(freq, i)
                phase_offset2 = self.phase_upsampler.value().next(phase_offset, i)
                phase = self.phasor.next(freq2, phase_offset2, trig_mask)
                sample = MFloat[self.num_chans](0.0)
                ref temp = self.world[].osc_buffers()
                comptime for chan in range(self.num_chans):
                    sample[chan] = temp.at_phase[osc_type, self.interp](self.world, phase[chan], self.last_phase[chan])
                self.downsampler.value().add_sample(sample)
                self.last_phase = phase

            return self.downsampler.value().get_sample()

    def sine(mut self, freq: MFloat[self.num_chans] = MFloat[self.num_chans](100.0), phase_offset: MFloat[self.num_chans] = MFloat[self.num_chans](0.0), trig: Bool = False) -> MFloat[self.num_chans]:
        """Generate the next sample of a sine wave oscillator.
        
        Args:
            freq: Frequency of the oscillator in Hz.
            phase_offset: Offsets the phase of the oscillator (0 to 1).
            trig: Trigger signal to reset the phase when switching from False to True (default is 0.0).

        Returns:
            The next sample of the oscillator output."""
        return self.next[OscType.sine](freq, phase_offset, trig)

    def triangle(mut self, freq: MFloat[self.num_chans] = MFloat[self.num_chans](100.0), phase_offset: MFloat[self.num_chans] = MFloat[self.num_chans](0.0), trig: Bool = False) -> MFloat[self.num_chans]:
        """Generate the next sample of a triangle wave oscillator.
        
        Args:
            freq: Frequency of the oscillator in Hz.
            phase_offset: Offsets the phase of the oscillator (0 to 1).
            trig: Trigger signal to reset the phase when switching from False to True (default is 0.0).

        Returns:
            The next sample of the oscillator output."""
        return self.next[OscType.triangle](freq, phase_offset, trig)

    def saw(mut self, freq: MFloat[self.num_chans] = MFloat[self.num_chans](100.0), phase_offset: MFloat[self.num_chans] = MFloat[self.num_chans](0.0), trig: Bool = False) -> MFloat[self.num_chans]:
        """Generate the next sample of a saw wave oscillator.
        
        Args:
            freq: Frequency of the oscillator in Hz.
            phase_offset: Offsets the phase of the oscillator (0 to 1).
            trig: Trigger signal to reset the phase when switching from False to True (default is 0.0).

        Returns:
            The next sample of the oscillator output."""
        return self.next[OscType.saw](freq, phase_offset, trig)

    def square(mut self, freq: MFloat[self.num_chans] = MFloat[self.num_chans](100.0), phase_offset: MFloat[self.num_chans] = MFloat[self.num_chans](0.0), trig: Bool = False) -> MFloat[self.num_chans]:
        """Generate the next sample of a square wave oscillator.
        
        Args:
            freq: Frequency of the oscillator in Hz.
            phase_offset: Offsets the phase of the oscillator (0 to 1).
            trig: Trigger signal to reset the phase when switching from False to True (default is 0.0).

        Returns:
            The next sample of the oscillator output."""
        return self.next[OscType.square](freq, phase_offset, trig)

    @always_inline
    def next_all_basic_waveforms(
            mut self,  
            phase: Float64 = 0.0,
            last_phase: Float64 = 0.0, 
            trig: Bool = False
        ) -> MFloat[4]:
        """Get the next sample of all basic waveforms (sine, triangle, saw, square) in a SIMD vector, where each waveform is in a different lane.

        Args:
            phase: Current oscillator phase.
            last_phase: Previous oscillator phase.
            trig: Trigger signal to reset the waveform phase.

        Returns:
            The next sample of the built-in basic waveforms.
        """

        ref temp = self.world[].osc_buffers()
        return temp.at_phase_basic_waveform[self.interp](self.world, phase, last_phase)

    @always_inline 
    def next_basic_waveforms[
        *osc_types: OscType
    ](
        mut self, 
        freq: MFloat[self.num_chans] = MFloat[self.num_chans](100.0), 
        phase_offset: MFloat[self.num_chans] = MFloat[self.num_chans](0.0), 
        trig: Bool = False, 
        osc_frac: MFloat[self.num_chans] = MFloat[self.num_chans](0.0) 
    ) -> MFloat[self.num_chans]:
        """Variable Wavetable Oscillator using built-in waveforms. Generates the next oscillator sample on a variable waveform where the output is interpolated between different waveform types.

        Parameters:
            osc_types: VariadicList of waveform types (OscType) to interpolate between. See the OscType struct for options. This should be indicated as a compile-time parameter pack, e.g. next_basic_waveforms[OscType.sine, OscType.triangle]. This cannot be left empty or it will cause a compile error.

        Args:
            freq: Frequency of the oscillator in Hz.
            phase_offset: Offsets the phase of the oscillator (default is 0.0).
            trig: Trigger signal to reset the phase when switching from False to True (default is 0.0).
            osc_frac: Fractional index for wavetable interpolation. Values are between 0.0 and 1.0. 0.0 corresponds to the first waveform in the osc_types list, 1.0 corresponds to the last waveform in the osc_types list, and values in between interpolate linearly between all waveforms in the list.

        Returns:
            The interpolated output sample.
        """
        
        # 2. Get the length of the parameter pack at compile time
        comptime num_osc_types = len(osc_types)
        comptime max_osc_frac = num_osc_types - 1
        
        var trig_mask = MBool[self.num_chans](fill=trig) 
        var scaled_osc_frac = Float64(max_osc_frac) * min(osc_frac, 1.0) 
        
        var osc_type0: MInt[self.num_chans] = MInt[self.num_chans](scaled_osc_frac) 
        var osc_type1 = MInt[self.num_chans](osc_type0 + 1) 
        
        osc_type0 = clip(osc_type0, 0, MInt[1](max_osc_frac)) 
        osc_type1 = clip(osc_type1, 0, MInt[1](max_osc_frac)) 

        # 4. Map the runtime vector indexes using the compile-time lookup array
        for i in range(self.num_chans): 
            osc_type0[i] = MInt[1](osc_types[Int(osc_type0[i])]._value) 
            osc_type1[i] = MInt[1](osc_types[Int(osc_type1[i])]._value) 
            
        osc_frac_interp = scaled_osc_frac - floor(scaled_osc_frac) 
        var out_sample = MFloat[self.num_chans](0.0) 

        comptime if Self.ov_samp == TimesOversampling.none:
            var phase = self.phasor.next(freq, phase_offset, trig_mask)
            comptime for chan in range(self.num_chans):
                sample = self.next_all_basic_waveforms(phase[chan], self.last_phase[chan], trig)
                out_sample[chan] = (MFloat[2](sample[Int(osc_type0[chan])], sample[Int(osc_type1[chan])]) * MFloat[2](1.0 - osc_frac_interp[chan], osc_frac_interp[chan])).reduce_add()
            self.last_phase = phase
            return out_sample
        else:
            comptime for i in range(Self.ov_samp.times):
                freq2 = self.freq_upsampler.value().next(freq, i)
                phase_offset2 = self.phase_upsampler.value().next(phase_offset, i)
                var phase = self.phasor.next(freq2, phase_offset2, trig_mask)
                comptime for chan in range(self.num_chans):
                    sample = self.next_all_basic_waveforms(phase[chan], self.last_phase[chan], trig)
                    out_sample[chan] = (MFloat[2](sample[Int(osc_type0[chan])], sample[Int(osc_type1[chan])]) * MFloat[2](1.0 - osc_frac_interp[chan], osc_frac_interp[chan])).reduce_add()
                self.downsampler.value().add_sample(out_sample)
                self.last_phase = phase
            return self.downsampler.value().get_sample()
   
    @always_inline
    def next_vwt(
            mut self: Osc, 
            ref buffer: Buffer, 
            freq: MFloat[self.num_chans] = MFloat[self.num_chans](100.0), 
            phase_offset: MFloat[self.num_chans] = MFloat[self.num_chans](0.0), 
            trig: Bool = False, 
            osc_frac: MFloat[self.num_chans] = MFloat[self.num_chans](0.0)
        ) -> MFloat[self.num_chans]:
        """Variable Wavetable Oscillator that interpolates over a loaded Buffer.
        Generates the next oscillator sample on a variable waveform where the output is interpolated between 
        different different channels of a provided Buffer.
        
        Args:
            buffer: Reference to a Buffer containing the waveforms to interpolate between.
            freq: Frequency of the oscillator in Hz.
            phase_offset: Offsets the phase of the oscillator (default is 0.0).
            trig: Trigger signal to reset the phase when switching from False to True (default is 0.0). All waveforms will reset together.
            osc_frac: Fractional index for wavetable interpolation. Values are between 0.0 and 1.0. 0.0 corresponds to the first channel in the input buffer, 1.0 corresponds to the last channel in the input buffer, and values in between interpolate linearly between all channels in the buffer.

        Returns:
            The interpolated wavetable sample.
        """
        var trig_mask = MBool[self.num_chans](fill=trig)

        var max_osc_frac = buffer.num_chans - 1

        var chan0_fl = Float64(max_osc_frac) * min(osc_frac, 1.0) #can't use a modulus here

        var buf_chan0: MInt[self.num_chans] = MInt[self.num_chans](chan0_fl)
        var buf_chan1 = MInt[self.num_chans](buf_chan0 + 1)

        scaled_osc_frac = chan0_fl - floor(chan0_fl)

        var sample0 = MFloat[self.num_chans](0.0)
        var sample1 = MFloat[self.num_chans](0.0)

        comptime if Self.ov_samp == TimesOversampling.none:
            # var last_phase = self.phasor.phase
            var phase = self.phasor.next(freq, phase_offset, trig_mask)
            comptime for out_chan in range(self.num_chans):
                    sample0[out_chan] = buffer.at_phase[self.interp, True, 0](self.world, Int(buf_chan0[out_chan]), phase[out_chan], self.last_phase[out_chan])
                    sample1[out_chan] = buffer.at_phase[self.interp, True, 0](self.world, Int(buf_chan1[out_chan]), phase[out_chan], self.last_phase[out_chan])
            self.last_phase = phase
            return linear_interp(sample0, sample1, scaled_osc_frac)
        else:
            comptime times_os_int = Self.ov_samp.times
            comptime for i in range(times_os_int):
                freq2 = self.freq_upsampler.value().next(freq, i)
                phase_offset2 = self.phase_upsampler.value().next(phase_offset, i)
                var phase = self.phasor.next(freq2, phase_offset2, trig_mask)
                comptime for out_chan in range(self.num_chans):
                    sample0[out_chan] = buffer.at_phase[self.interp, True, 0](self.world, Int(buf_chan0[out_chan]), phase[out_chan], self.last_phase[out_chan])
                    sample1[out_chan] = buffer.at_phase[self.interp, True, 0](self.world, Int(buf_chan1[out_chan]), phase[out_chan], self.last_phase[out_chan])
                self.downsampler.value().add_sample(linear_interp(sample0, sample1, scaled_osc_frac))
                self.last_phase = phase
            return self.downsampler.value().get_sample()

    @always_inline
    def next_vwt[simd_chans: Int](
            mut self: Osc, 
            ref buffer: SIMDBuffer[simd_chans], 
            freq: MFloat[self.num_chans] = MFloat[self.num_chans](100.0), 
            phase_offset: MFloat[self.num_chans] = MFloat[self.num_chans](0.0), 
            trig: Bool = False, 
            osc_frac: MFloat[self.num_chans] = MFloat[self.num_chans](0.0)
        ) -> MFloat[self.num_chans]:
        """Variable Wavetable Oscillator that interpolates over a loaded SIMDBuffer.
        Generates the next oscillator sample on a variable waveform where the output is interpolated between 
        different different channels of a provided Buffer. This should only be used with low channel counts (maybe up to 4 or 8 channels depending on the CPU).

        Parameters:
            simd_chans: Number of channels stored in the source SIMDBuffer.
        
        Args:
            buffer: Reference to a Buffer containing the waveforms to interpolate between.
            freq: Frequency of the oscillator in Hz.
            phase_offset: Offsets the phase of the oscillator (default is 0.0).
            trig: Trigger signal to reset the phase when switching from False to True (default is 0.0). All waveforms will reset together.
            osc_frac: Fractional index for wavetable interpolation. Values are between 0.0 and 1.0. 0.0 corresponds to the first channel in the input buffer, 1.0 corresponds to the last channel in the input buffer, and values in between interpolate linearly between all channels in the buffer.

        Returns:
            The interpolated wavetable sample.
        """
        var trig_mask = MBool[self.num_chans](fill=trig)

        var max_osc_frac = buffer.num_chans - 1

        var chan0_fl = Float64(max_osc_frac) * min(osc_frac, 1.0) #can't use a modulus here

        var buf_chan0: MInt[self.num_chans] = MInt[self.num_chans](chan0_fl)
        var buf_chan1 = MInt[self.num_chans](buf_chan0 + 1)

        scaled_osc_frac = chan0_fl - floor(chan0_fl)

        comptime if Self.ov_samp == TimesOversampling.none:
            var phase = self.phasor.next(freq, phase_offset, trig_mask)
            out_sample = MFloat[self.num_chans](0.0)
            comptime for out_chan in range(self.num_chans):
                sample = buffer.at_phase[self.interp, True, 0](self.world, phase[out_chan], self.last_phase[out_chan])
                out_sample[out_chan] = (MFloat[2](sample[Int(buf_chan0[out_chan])], sample[Int(buf_chan1[out_chan])]) * MFloat[2](1.0 - scaled_osc_frac[out_chan], scaled_osc_frac[out_chan])).reduce_add()
                
            self.last_phase = phase
            return out_sample
        else:
            comptime times_os_int = Self.ov_samp.times
            comptime for i in range(times_os_int):
                freq2 = self.freq_upsampler.value().next(freq, i)
                phase_offset2 = self.phase_upsampler.value().next(phase_offset, i)
                var phase = self.phasor.next(freq2, phase_offset2, trig_mask)
                out_sample = MFloat[self.num_chans](0.0)
                comptime for out_chan in range(self.num_chans):
                    sample = buffer.at_phase[self.interp, True, 0](self.world, phase[out_chan], self.last_phase[out_chan])
                    out_sample[out_chan] = (MFloat[2](sample[Int(buf_chan0[out_chan])], sample[Int(buf_chan1[out_chan])]) * MFloat[2](1.0 - scaled_osc_frac[out_chan], scaled_osc_frac[out_chan])).reduce_add()
                self.downsampler.value().add_sample(out_sample)
                self.last_phase = phase
            return self.downsampler.value().get_sample()

struct OscBank[num: Int](Movable, Copyable):
    """A convenience struct for processing a bank of Osc's with fixed frequencies. The number are indicated by the num parameter. Each output can be accessed via index.

    Parameters:
        num: The total number of Oscillators.
    """
    comptime simd_width = simd_width_of[DType.float64]() * 2
    comptime num_simd = Self.num // Self.simd_width + (1 if Self.num % Self.simd_width != 0 else 0)
    var oscs : List[Osc[Self.simd_width]]
    var freqs: List[MFloat[Self.simd_width]]

    def __init__(out self, world: World):
        self.oscs = [Osc[Self.simd_width](world) for _ in range(Self.num_simd)]
        self.freqs = [MFloat[Self.simd_width](rrand(100.0, 2000.0)) for _ in range(Self.num_simd)]

    def set_freq(mut self, index: Int, freq: Float64):
        """Set the frequency of a specific oscillator in the bank.

        Args:
            index: Index of the oscillator to retune.
            freq: Frequency in Hertz to assign.
        """
        simd_index = index // Self.simd_width
        lane_index = index % Self.simd_width
        if simd_index < Self.num_simd:
            self.freqs[simd_index][lane_index] = freq

    def next[osc_type: OscType = OscType.sine](mut self) -> Float64:
        """Generate the next sample from the oscillator bank.

        Parameters:
            osc_type: Waveform type used by each oscillator in the bank.

        Returns:
            The averaged output of all oscillators in the bank.
        """
        out = MFloat[Self.simd_width](0.0)
        for i in range(Self.num_simd):
            out += self.oscs[i].next[osc_type=osc_type](self.freqs[i])
        return out.reduce_add() / Float64(Self.num) 

struct LFOsc[num_chans: Int = 1, ov_samp: TimesOversampling = TimesOversampling.none] (Movable, Copyable):
    """A low-frequency oscillator with multiple waveform options.
    
    This oscillator generates a non-bandlimited oscillator capable of saw, triangle, and square waves. It is useful for modulation, but should be avoided for audio-rate synthesis due to aliasing.

    Outputs values between -1.0 and 1.0.

    Parameters:
        num_chans: Number of channels (default is 1).
        ov_samp: How many times oversampling (default is TimesOversampling.none).
    """

    var phasor: Phasor[Self.num_chans]
    var world: World 
    var downsampler: Optional[Downsampler[Self.num_chans, Self.ov_samp]]

    def __init__(out self, world: World):
        """Initialize the low-frequency oscillator.

        Args:
            world: Pointer to the MMMWorld instance.
        """
        self.phasor = Phasor[self.num_chans](world)
        self.world = world

        if Self.ov_samp == TimesOversampling.none:
            self.phasor = Phasor[self.num_chans](world)
            self.downsampler = None
        else:
            oversampled_world = world[].create_subworld(Self.ov_samp)
            self.phasor = Phasor[self.num_chans](oversampled_world)
            self.downsampler = Downsampler[self.num_chans, Self.ov_samp](world)

    @always_inline
    def next[osc_type: OscType = OscType.saw](mut self, freq: MFloat[self.num_chans] = 100.0, phase_offset: MFloat[self.num_chans] = 0.0, trig: Bool = False) -> MFloat[self.num_chans]:
        """Generate the next sawtooth wave sample.

        Parameters:
            osc_type: Waveform type to generate.

        Args:
            freq: Frequency of the sawtooth wave in Hz.
            phase_offset: Offsets the phase of the oscillator (default is 0.0).
            trig: Trigger signal to reset the phase when switching from False to True (default is 0.0).

        Returns:
            The next low-frequency oscillator sample.
        """

        var trig_mask = MBool[self.num_chans](fill=trig)
        out = MFloat[self.num_chans](0.0)
        comptime for _ in range(Self.ov_samp.times):
            comptime if osc_type == OscType.saw:
                out = 1.0 - 2.0 * self.phasor.next(freq, phase_offset, trig_mask)
            elif osc_type == OscType.triangle:
                out = (abs((self.phasor.next(freq, phase_offset, trig_mask) * 4.0) - 2.0) - 1.0)
            elif osc_type == OscType.square:
                mask = self.phasor.next(freq, phase_offset, trig_mask).lt(0.5)
                out = mask.select(1.0, -1.0)
            else:
                phase = self.phasor.next(freq, phase_offset, trig_mask)
                out = sin(phase * two_pi)
            comptime if Self.ov_samp != TimesOversampling.none:
                self.downsampler.value().add_sample(out)

        comptime if Self.ov_samp == TimesOversampling.none:
            return out
        else:
            return self.downsampler.value().get_sample()

    def sine(mut self, freq: MFloat[self.num_chans] = MFloat[self.num_chans](100.0), phase_offset: MFloat[self.num_chans] = MFloat[self.num_chans](0.0), trig: Bool = False) -> MFloat[self.num_chans]:
        """Generate the next sample of a sine wave oscillator.
        
        Args:
            freq: Frequency of the oscillator in Hz.
            phase_offset: Offsets the phase of the oscillator (0 to 1).
            trig: Trigger signal to reset the phase when switching from False to True (default is 0.0).

        Returns:
            The next sample of the oscillator output."""
            
        return self.next[OscType.sine](freq, phase_offset, trig)

    def triangle(mut self, freq: MFloat[self.num_chans] = MFloat[self.num_chans](100.0), phase_offset: MFloat[self.num_chans] = MFloat[self.num_chans](0.0), trig: Bool = False) -> MFloat[self.num_chans]:
        """Generate the next sample of a triangle wave oscillator.
        
        Args:
            freq: Frequency of the oscillator in Hz.
            phase_offset: Offsets the phase of the oscillator (0 to 1).
            trig: Trigger signal to reset the phase when switching from False to True (default is 0.0).

        Returns:
            The next sample of the oscillator output."""
        return self.next[OscType.triangle](freq, phase_offset, trig)

    def saw(mut self, freq: MFloat[self.num_chans] = MFloat[self.num_chans](100.0), phase_offset: MFloat[self.num_chans] = MFloat[self.num_chans](0.0), trig: Bool = False) -> MFloat[self.num_chans]:
        """Generate the next sample of a saw wave oscillator.
        
        Args:
            freq: Frequency of the oscillator in Hz.
            phase_offset: Offsets the phase of the oscillator (0 to 1).
            trig: Trigger signal to reset the phase when switching from False to True (default is 0.0).

        Returns:
            The next sample of the oscillator output."""
        return self.next[OscType.saw](freq, phase_offset, trig)

    def square(mut self, freq: MFloat[self.num_chans] = MFloat[self.num_chans](100.0), phase_offset: MFloat[self.num_chans] = MFloat[self.num_chans](0.0), trig: Bool = False) -> MFloat[self.num_chans]:
        """Generate the next sample of a square wave oscillator.
        
        Args:
            freq: Frequency of the oscillator in Hz.
            phase_offset: Offsets the phase of the oscillator (0 to 1).
            trig: Trigger signal to reset the phase when switching from False to True (default is 0.0).

        Returns:
            The next sample of the oscillator output."""
        return self.next[OscType.square](freq, phase_offset, trig)

struct Dust[num_chans: Int = 1] (Movable, Copyable):
    """A dust noise oscillator that generates random impulses at random intervals.
    
    Dust has a Phasor as its core, and the frequency of the Phasor is randomly changed each time an impulse is generated. This allows the Dust to be used in multiple ways. It can be used as a simple random impulse generator, or the user can use the get_phase() method to get the current phase of the internal Phasor and use that phase to drive other oscillators or processes. The user can also set the phase of the internal Phasor using the set_phase() method, allowing for more complex interactions.

    Parameters:
        num_chans: Number of channels.
    """
    var impulse: Phasor[Self.num_chans]
    var freq: MFloat[Self.num_chans]

    def __init__(out self, world: World):
        """Initialize the dust noise oscillator.

        Args:
            world: Pointer to the MMMWorld instance.
        """
        self.impulse = Phasor[Self.num_chans](world)
        # this will cause all Dusts to start at a different phase
        for i in range(self.num_chans):
            self.impulse.phase[i] = rrand(0.0, 1.0)
        self.freq = MFloat[Self.num_chans](1.0)

    def next(mut self: Dust, low: MFloat[self.num_chans] = 100.0, high: MFloat[self.num_chans] = 2000.0, trig: MBool[self.num_chans] = MBool[self.num_chans](fill= False)) -> MFloat[self.num_chans]:
        """Generate the next dust noise sample.
        
        Args:
            low: Lower bound for the random frequency range.
            high: Upper bound for the random frequency range.
            trig: Trigger signal to reset the phase when switching from False to True.

        Returns:
            The next dust noise sample as a Float64. Will be 1.0 when an impulse occurs, 0.0 otherwise.
        """
        return self.next_bool(low, high, trig).cast[DType.float64]()

    @always_inline
    def next_bool(mut self: Dust, low: MFloat[self.num_chans] = 100.0, high: MFloat[self.num_chans] = 2000.0, trig: MBool[self.num_chans] = MBool[self.num_chans](fill= False)) -> MBool[self.num_chans]:
        """Generate the next dust noise sample as a boolean impulse.
        
        Args:
            low: Lower bound for the random frequency range.
            high: Upper bound for the random frequency range.
            trig: Trigger signal to reset the phase when switching from False to True.

        Returns:
            The next dust noise sample as a boolean SIMD. Will be True when an impulse occurs, False otherwise.
        """

        var tick = self.impulse.next_bool(self.freq, 0, trig)  # Update the phase

        comptime for i in range(self.num_chans):
            if tick[i]:
                self.freq[i] = random_float64(low[i], high[i])

        return tick

    def get_phase(self) -> MFloat[self.num_chans]:
        return self.impulse.phase

    def set_phase(mut self, phase: MFloat[self.num_chans]):
        self.impulse.phase = phase


struct TTrig(Movable, Copyable):
    """A trigger that outputs True for a specified number of samples or amount of time after receiving a trigger signal."""

    var counter: Int
    var world: World

    def __init__(out self, world: World):
        self.counter = 0
        self.world = world

    def next(mut self, trig: Bool, samples: Int) -> Bool:
        """Generate the next trigger sample.
        
        Args:
            trig: Trigger signal.
            samples: Number of samples for which to output True.

        Returns:
            True if the trigger is active, False otherwise.
        """
        if trig:
            self.counter = samples
        if self.counter <=0:
            return False
        else:
            self.counter -= 1
            return True
    
    def next(mut self, trig: Bool, time: MFloat[1]) -> Bool:
        """Generate the next trigger sample based on time.

        Args:
            trig: Trigger signal.
            time: Amount of time in seconds for which to output True.

        Returns:
            True if the trigger is active, False otherwise.
        """
        if trig:
            self.counter = Int(time * self.world[].sample_rate)
        return self.next(False, 0)

struct LFNoise[num_chans: Int = 1, interp: Interp = Interp.cubic](Movable, Copyable):
    """Low-frequency interpolating noise generator generating numbers between -1.0 and 1.0. With stepped (none), linear, or cubic interpolation.

    Parameters:
        num_chans: Number of channels.
        interp: Interpolation method. Options are Interp.none (stepped), Interp.linear, Interp.cubic.
    """
    var impulse: Phasor[Self.num_chans]

    # Cubic inerpolation only needs 4 points, but it needs to know the true previous point so the history
    # needs an extra point: the 4 for interpolation, plus the point that is just changed
    var history: List[MFloat[Self.num_chans]]# used for interpolation

    # history_index: the index of the history list that the impulse's phase is moving *away* from
    # phase is moving *towards* history_index + 1
    var history_index: List[Int]

    def __init__(out self, world: World):
        """Initialize the low-frequency noise generator.

        Args:
            world: Pointer to the MMMWorld instance.
        """
        self.history_index = [0 for _ in range(self.num_chans)]
        self.impulse = Phasor[Self.num_chans](world)
        self.history = [MFloat[Self.num_chans](0.0) for _ in range(5)]
        for i in range(Self.num_chans):
            for j in range(len(self.history)):
                self.history[j][i] = random_float64(0.1, 1.0)
        # Initialize history with random values

    @always_inline
    def next(mut self: LFNoise, freq: MFloat[self.num_chans] = 100.0) -> MFloat[self.num_chans]:
        """Generate the next low-frequency noise sample.
        
        Args:
            freq: Frequency of the noise in Hz.

        Returns:
            The next sample as a Float64.
        """
        # var trig_mask = MBool[self.num_chans](fill=False)
        var tick = self.impulse.next_bool(freq)  # Update the phase

        comptime for i in range(self.num_chans):
            if tick[i]:  # If an impulse is detected
                # advance the history index
                self.history_index[i] = (self.history_index[i] + 1) % len(self.history)

            # so don't change that one, cubic interp needs to know that, so we'll change 
            # history_index - 2 (but, again, computed differently to avoid negative indices) so
            # the next time we wrap around to that part of the history list it will be a new random value
            self.history[(self.history_index[i] + (len(self.history) - 2)) % len(self.history)][i] = random_float64(-1.0, 1.0)

        comptime if Self.interp == Interp.none:
            p0 = MFloat[self.num_chans](0.0)
            comptime for i in range(self.num_chans):
                p0[i] = self.history[(self.history_index[i] + 1) % len(self.history)][i]
            return p0
        elif Self.interp == Interp.linear:
            # Linear interpolation between last and next value
            p0 = MFloat[self.num_chans](0.0)
            p1 = MFloat[self.num_chans](0.0)
            comptime for i in range(Self.num_chans):
                p0[i] = self.history[self.history_index[i]][i]
                p1[i] = self.history[(self.history_index[i] + 1) % len(self.history)][i]
            return linear_interp(p0, p1, self.impulse.phase)
        else:
            p0 = MFloat[self.num_chans](0.0)
            p1 = MFloat[self.num_chans](0.0)
            p2 = MFloat[self.num_chans](0.0)
            p3 = MFloat[self.num_chans](0.0)
            comptime for i in range(self.num_chans):
                p0[i] = self.history[(self.history_index[i] + (len(self.history) - 1)) % len(self.history)][i]
                p1[i] = self.history[self.history_index[i]][i]
                p2[i] = self.history[(self.history_index[i] + 1) % len(self.history)][i]
                p3[i] = self.history[(self.history_index[i] + 2) % len(self.history)][i]
            # Cubic interpolation
            return cubic_interp(p0, p1, p2, p3, self.impulse.phase)

struct Sweep[num_chans: Int = 1](Movable, Copyable):
    """A phase accumulator.
    
    Phase accumulator that sweeps from 0 up to inf at a given frequency, resetting on trigger.

    Parameters:
        num_chans: Number of channels.
    """

    var phase: MFloat[Self.num_chans]
    var freq_mul: Float64
    var rising_bool_detector: RisingBoolDetector[Self.num_chans]  # Track the last reset state
    var world: World  # Pointer to the MMMWorld instance

    def __init__(out self, world: World):
        """Initialize the sweep generator.

        Args:
            world: Pointer to the MMMWorld instance.
        """
        self.world = world
        self.phase = MFloat[Self.num_chans](0.0)
        self.freq_mul = 1.0 / self.world[].sample_rate
        self.rising_bool_detector = RisingBoolDetector[Self.num_chans]()
   
    @always_inline
    def next(mut self, freq: MFloat[self.num_chans] = 100.0, trig: MBool[self.num_chans] = False) -> MFloat[self.num_chans]:
        """Generate the next sweep sample.

        Args:
            freq: Frequency of the sweep in Hz.
            trig: Trigger signal to reset the phase when switching from False to True (default is all False).

        Returns:
            The next sample as a Float64.
        """

        self.phase += (freq * self.freq_mul)

        var resets = self.rising_bool_detector.next(trig)

        self.phase = resets.select(0.0, self.phase)

        return self.phase

struct Line[num_chans: Int = 1](Movable, Copyable):
    """
        A Line between start and end that takes dur seconds to complete. Line has 3 modes: linear, exponential, and curve. The standard .next function is linear, .exp is exponential, and .curve uses lincurve to warp the output.
    """

    var phase: MFloat[Self.num_chans]
    var freq_mul: Float64
    var rising_bool_detector: RisingBoolDetector[Self.num_chans]  # Track the last reset state
    var world: World  # Pointer to the MMMWorld instance
    var freq: MFloat[Self.num_chans]
    var val: MFloat[Self.num_chans]

    def __init__(out self, world: World):
        """Initialize the line generator.

        Args:
            world: Pointer to the MMMWorld instance.
        """
        self.world = world
        self.phase = MFloat[Self.num_chans](0.0)
        self.freq_mul = 1.0 / self.world[].sample_rate
        self.rising_bool_detector = RisingBoolDetector[Self.num_chans]()
        self.freq = MFloat[Self.num_chans](0.0)
        self.val = MFloat[Self.num_chans](0.0)

    @always_inline
    def next(mut self, start: MFloat[self.num_chans], end: MFloat[self.num_chans], dur: MFloat[self.num_chans], trig: MBool[self.num_chans] = True) -> MFloat[self.num_chans]:
        """Generate the next line sample.

        Args:
            start: Starting value of the line.
            end: Ending value of the line.
            dur: Duration of the line in seconds.
            trig: Trigger signal to reset the phase when switching from False to True (default is all False).

        Returns:
            The next interpolated line value.
        """
        self.phase += (self.freq * self.freq_mul)
        var resets = self.rising_bool_detector.next(trig)
        self.freq = resets.select((1.0 / dur), self.freq)
        self.phase = resets.select(0.0, self.phase)
        self.phase = clip(self.phase, 0.0, 1.0)

        self.val = linlin(self.phase, 0.0, 1.0, start, end)
        return self.val
    
    def exp(mut self, start: MFloat[self.num_chans], end: MFloat[self.num_chans], dur: MFloat[self.num_chans], trig: MBool[self.num_chans] = True) -> MFloat[self.num_chans]:
        """Generate the next exponential line sample.

        Args:
            start: Starting value of the line.
            end: Ending value of the line.
            dur: Duration of the line in seconds.
            trig: Trigger signal to reset the phase when switching from False to True (default is all False).

        Returns:
            The next line value shaped to an exponential curve.
        """
        self.val = linexp(self.next(0., 1., dur, trig), 0.0, 1.0, start, end)
        return self.val

    def curve(mut self, start: MFloat[self.num_chans], end: MFloat[self.num_chans], dur: MFloat[self.num_chans], trig: MBool[self.num_chans] = True, curve: MFloat[self.num_chans] = 2.0) -> MFloat[self.num_chans]:
        """Generate the next curved line sample shaped by lincurve.

        Args:
            start: Starting value of the line.
            end: Ending value of the line.
            dur: Duration of the line in seconds.
            trig: Trigger signal to reset the phase when switching from False to True (default is all False).
            curve: The curve factor for the interpolation. A value of 1.0 results in a linear interpolation, values greater than 1.0 result in an exponential curve, and values less than 1.0 result in a logarithmic curve.

        Returns:
            The next line value shaped by a lincurve.
        """
        self.val = lincurve(self.next(0., 1., dur, trig), 0.0, 1.0, start, end, curve)
        return self.val


comptime OscBuffersSize: Int = 16384  # 2^14
comptime OscBuffersMask: Int = 16383  # 2^14 - 1

@doc_hidden
struct OscBuffers(Movable, Copyable):
    var sine_buffer: SIMDBuffer[1]
    var triangle_buffer: SIMDBuffer[1]
    var saw_buffer: SIMDBuffer[1]
    var square_buffer: SIMDBuffer[1]
    var basic_waveforms: SIMDBuffer[4]

    def at_phase[osc_type: OscType, interp: Interp = Interp.none](self, world: World, phase: Float64, prev_phase: Float64 = 0) -> Float64:
        comptime if osc_type == OscType.sine:
            return self.sine_buffer.at_phase[interp=interp, bWrap=True, mask=OscBuffersMask](world, phase, prev_phase)
        elif osc_type == OscType.triangle:
            return self.triangle_buffer.at_phase[interp=interp, bWrap=True, mask=OscBuffersMask](world, phase, prev_phase)
        elif osc_type == OscType.saw:
            return self.saw_buffer.at_phase[interp=interp, bWrap=True, mask=OscBuffersMask](world, phase, prev_phase)
        elif osc_type == OscType.square:
            return self.square_buffer.at_phase[interp=interp, bWrap=True, mask=OscBuffersMask](world, phase, prev_phase)
        else:
            return 0.0

    def at_phase_basic_waveform[interp: Interp = Interp.none](self, world: World, phase: Float64, prev_phase: Float64 = 0) -> MFloat[4]:
        return self.basic_waveforms.at_phase[interp=interp, bWrap=True, mask=OscBuffersMask](world, phase, prev_phase)

    @doc_hidden
    def __init__(out self):
        self.basic_waveforms = SIMDBuffer[4].zeros(OscBuffersSize)
        self.sine_buffer = SIMDBuffer[1].zeros(OscBuffersSize)
        self.triangle_buffer = SIMDBuffer[1].zeros(OscBuffersSize)
        self.saw_buffer = SIMDBuffer[1].zeros(OscBuffersSize)
        self.square_buffer = SIMDBuffer[1].zeros(OscBuffersSize)
        
        self.init_sine()  
        self.init_triangle()
        self.init_sawtooth()
        self.init_square()

    # Build Wavetables:
    # =================
    @doc_hidden
    def init_sine(mut self):
        for i in range(OscBuffersSize):
            v = sin(2.0 * 3.141592653589793 * Float64(i) / Float64(OscBuffersSize))
            self.sine_buffer.data[i] = v
            self.basic_waveforms.data[i][0] = v

    @doc_hidden
    def init_triangle(mut self):
        # Construct triangle wave from sine harmonics
        # Triangle formula: 8/pi^2 * sum((-1)^(n+1) * sin(n*x) / n^2) for n=1 to 512
        for i in range(OscBuffersSize):
            var x = 2.0 * 3.141592653589793 * Float64(i) / Float64(OscBuffersSize)
            var sample: Float64 = 0.0
            
            for n in range(1, 513):  # Using 512 harmonics
                var harmonic = sin(Float64(2 * n - 1) * x) / (Float64(2 * n - 1) * Float64(2 * n - 1))
                if n % 2 == 0:  # (-1)^(n+1) is -1 when n is even
                    harmonic = -harmonic

                sample += harmonic
            
            # Scale by 8/π² for correct amplitude
            self.triangle_buffer.data[i] = 8.0 / (3.141592653589793 * 3.141592653589793) * sample
            self.basic_waveforms.data[i][1] = self.triangle_buffer.data[i]

    @doc_hidden
    def init_sawtooth(mut self):
        # Construct sawtooth wave from sine harmonics
        # Sawtooth formula: 2/pi * sum((-1)^(n+1) * sin(n*x) / n) for n=1 to 512
        for i in range(OscBuffersSize):
            var x = 2.0 * 3.141592653589793 * Float64(i) / Float64(OscBuffersSize)
            var sample: Float64 = 0.0
            
            for n in range(1, 513):  # Using 512 harmonics
                var harmonic = sin(Float64(n) * x) / Float64(n)
                sample += harmonic
            
            # Scale by 2/π for correct amplitude
            self.saw_buffer.data[i] = 2.0 / 3.141592653589793 * sample
            self.basic_waveforms.data[i][2] = self.saw_buffer.data[i]

    @doc_hidden
    def init_square(mut self):
        # Construct square wave from sine harmonics
        # Square formula: 4/pi * sum(sin((2n-1)*x) / (2n-1)) for n=1 to 512
        for i in range(OscBuffersSize):
            var x = 2.0 * 3.141592653589793 * Float64(i) / Float64(OscBuffersSize)
            var sample: Float64 = 0.0
            
            for n in range(1, 513):  # Using 512 harmonics
                var harmonic = sin(Float64(2 * n - 1) * x) / Float64(2 * n - 1)
                sample += harmonic
            
            # Scale by 4/π for correct amplitude
            self.square_buffer.data[i] = 4.0 / 3.141592653589793 * sample
            self.basic_waveforms.data[i][3] = self.square_buffer.data[i]
